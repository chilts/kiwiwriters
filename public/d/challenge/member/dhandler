<%once>
    use GD;
    my $winner = GD::Image->new("/usr/share/kiwiwriters/banners/member-winner-120x90.png");
    my $participant = GD::Image->new("/usr/share/kiwiwriters/banners/member-participant-120x90.png");
</%once>
<%init>
    my ($e_name, $type) = $m->dhandler_arg() =~ m{ \A ([\w-]+)-(winner|participant) \. png \z }xms;

    unless ( $type ) {
        $m->clear_buffer();
        $m->abort(404);
        return;
    }

    # make sure this event exists
    my $model = $ZAAPT->get_model("KiwiWriters::Zaapt::Store::Pg::Challenge");
    my $event = $model->sel_event_using_name({ e_name => $e_name });

    unless ( defined $event ) {
        $m->clear_buffer();
        $m->abort(404);
        return;
    }

    $r->content_type('image/png');

    # get the three lines of text
    my @lines = split m{\n}xms, $event->{e_title};

    my $image = ($type eq 'winner') ? $winner->clone() : $participant->clone();
    my $color = $image->colorAllocate(0,0,0);

    for (my $i = 0; $i < @lines; $i++ ) {
        $image->string(gdSmallFont, 60 - ((length $lines[$i])/2*6), 16+$i*12, $lines[$i], $color);
    }

    binmode STDOUT;
    print $image->png();

    return;
</%init>
