%# ----------------------------------------------------------------------------
<%args>
    $action    => ''
    $firstname => ''
    $lastname  => ''
    $email     => ''
    $notify    => 1
    $password  => ''
    $repeat    => ''
</%args>
%# ----------------------------------------------------------------------------
<%init>
    my $model = $ZAAPT->get_model('Account');
    my $account = $model->sel_account({ a_id => $SESS->{id} });

    my $err_info = [];
    my $err_pass = [];

    if ( $action eq 'update' ) {
        # turn notify into a boolean
        $notify = ( $notify eq '1' ? 1 : 0 );

        my $empty = 0;
        foreach ( $firstname, $lastname, $email ) {
            $empty++ unless m{ \S }xms;
        }
        push @$err_info, "At least one field is blank, please enter all fields" if $empty;

        unless ( @$err_info ) {
            my $model = $ZAAPT->get_model('Account');
            $model->upd_account({
                a_firstname => $firstname,
                a_lastname  => $lastname,
                a_email     => $email,
                a_notify    => $notify,
                a_id        => $SESS->{id},
            });
            $SESS->{firstname} = $firstname;
            $SESS->{lastname} = $lastname;
            $SESS->{email} = $email;
            $SESS->{notify} = $notify;
            $m->clear_buffer();
            $m->redirect('?');
            return;
        }
    }
    elsif ( $action eq 'password' ) {
        # check the passwords are okay

        my $empty = 0;
        foreach ( $password, $repeat ) {
            $empty++ unless m{ \S }xms;
        }
        push @$err_pass, "At least one field is blank, please enter all fields" if $empty;

        unless ( $password eq $repeat ) {
            push @$err_pass, "Password and the repeat are not the same, please re-type";
        }

        unless ( length $password >= 6 ) {
            push @$err_pass, "Password must be at least 6 characters long.";
        }

        unless ( @$err_pass ) {
            my $model = $ZAAPT->get_model('Account');
            $model->upd_password({
                a_password => $password,
                a_id       => $SESS->{id},
            });
            $m->clear_buffer();
            $m->redirect('?');
            return;
        }
    }
</%init>
%# ----------------------------------------------------------------------------
<h1>Edit your Account Info</h1>

<& /zaapt/common/err.mhtml, err => $err_info &>

<& /zaapt/common/form.mhtml, action => '?', elements => [
        { type => 'hidden', name => 'action', value => 'update' },
        { type => 'text', field => 'Firstname', name => 'firstname', size => 30, maxlength => 30, value => $account->{a_firstname} },
        { type => 'text', field => 'Lastname', name => 'lastname', size => 30, maxlength => 30, value => $account->{a_lastname} },
        { type => 'text', field => 'Email', name => 'email', size => 30, maxlength => 30, value => $account->{a_email} },
        {
            type => 'radio',
            field => 'Receive Site Notifications',
            name => 'notify',
            options => [ { name => 'Yes', value => 1 }, { name => 'No', value => 0 } ],
            checked => $account->{a_notify},
        },
        { type => 'submit', field => '', value => 'Save Changes' },
    ]
&>

<h1>Change Password</h1>

<& /zaapt/common/err.mhtml, err => $err_pass &>

<& /zaapt/common/form.mhtml, action => '?', elements => [
        { type => 'hidden', name => 'action', value => 'password' },
        { type => 'password', field => 'Password', name => 'password', size => 30, maxlength => 30, value => '' },
        { type => 'password', field => 'Repeat', name => 'repeat', size => 30, maxlength => 30, value => '' },
        { type => 'submit', field => '', value => 'Save Changes' },
    ]
&>

% if ( 0 ) {
<pre>
% print Data::Dumper->Dump([$account], ['a']);
</pre>
% }
%# ----------------------------------------------------------------------------
<%attr>
    done_today => 1
</%attr>
%# ----------------------------------------------------------------------------
