%# ----------------------------------------------------------------------------
<%args>
    $act     => undef
    $name    => undef
    $email   => undef
    $to      => 'contact'
    $subject => undef
    $message => undef
</%args>
%# ----------------------------------------------------------------------------
<%once>
    use Mail::Sendmail qw();
    my $contacts = {
        contact => {
            email   => 'contact@kiwiwriters.org',
            display => 'contact at kiwiwriters dot org',
            what    => 'General contact questions or information',
        },
        webmaster => {
            email   => 'webmaster@kiwiwriters.org',
            display => 'webmaster at kiwiwriters dot org',
            what    => 'Website queries, bugs or suggestions',
        },
    };
    my @options;
    foreach my $to ( qw(contact webmaster) ) {
        push @options, { value => $to, name => "<strong>$contacts->{$to}{what}</strong><br />$contacts->{$to}{display}" };
    }
</%once>
%# ----------------------------------------------------------------------------
<%init>
    my $err = [];
    if ( $act eq 'send' ) {
        unless ( exists $contacts->{$to} ) {
            push @$err, 'unknown to address';
        }
        unless ( $name =~ m{ \S }xms ) {
            push @$err, 'please tell us your name';
        }
        unless ( $subject =~ m{ \S }xms ) {
            push @$err, 'please type a subject';
        }
        unless ( $message =~ m{ \S }xms ) {
            push @$err, 'please type a message';
        }

        $message =~ s{\r}{}gxms;

        unless ( @$err ) {
            my %mail;

            # send this message to us
            %mail = (
                To => $contacts->{$to}{email},
                From => $email,
                Subject => $subject,
                Message => $message,
                'X-Mailer' => "Mail::Sendmail version $Mail::Sendmail::VERSION",
            );

            if ( Mail::Sendmail::sendmail(%mail) ) {
                $m->redirect('?act=sent');
                $name = $email = $subject = $message = undef;
            }
            else {
                warn "Error sending mail: $Mail::Sendmail::error";
                push @$err, "there was an error sending your mail, please try again later";
            }

        }
    }

</%init>
%# ----------------------------------------------------------------------------
<h1>Contact Us</h1>

<& /zaapt/common/err.mhtml, err => $err &>

% if ( $act eq 'sent' ) {
<p>Thank you very much for your feedback.</p>
% }

<p>Please fill in the details in this form, or just email any of the addresses as shown below.</p>

<& /zaapt/common/form.mhtml, action => '?', elements => [
        { type => 'hidden', name => 'act', value => 'send' },
        { type => 'radio', field => 'To', name => 'to', br => 1, options => \@options, checked => $to, noesc => 1 },
        { type => 'text', field => 'Your Name', name => 'name', size => 40, value => $name },
        { type => 'text', field => 'Your Email', name => 'email', size => 40, value => $email },
        { type => 'text', field => 'Subject', name => 'subject', size => 40, value => $subject },
        { type => 'textarea', field => 'Message', name => 'message', rows => 10, cols => 40, value => $message },
        { type => 'submit', field => 'Actions', value => 'Send Message' },
    ]
&>

%# ----------------------------------------------------------------------------
